Program NewLine Variable VariableTail Value DeclarationList DeclarationListTail Declaration DeclarationTail OptionalAssignment Primitive ArrayDeclaration Type CompoundStatement StatementList Statement StatementTail AssignStatement Factor Expression ExpressionTail Term TermTail IOStatement IfStatement WhileStatement Conditions ConditionsTail Condition ConditionTail SimpleCondition Relation
Identifier Constant number char string array input output if else while + - * / % = == != < > <= >= && \d ( ) [ ] { } : , \n
Program

Program -> StatementList
NewLine -> \n

Variable -> Identifier VariableTail
VariableTail -> [ Expression ] | ε
Value -> Constant | Variable

DeclarationList -> Declaration DeclarationListTail
DeclarationListTail -> , Declaration DeclarationListTail | ε

Declaration -> Identifier : DeclarationTail
DeclarationTail -> Type OptionalAssignment
OptionalAssignment -> = Expression | ε

Primitive -> number | char | string
ArrayDeclaration -> array < Primitive > ( Expression )
Type -> Primitive | ArrayDeclaration

CompoundStatement -> { StatementList }
StatementList -> Statement NewLine StatementList | ε

Statement -> Identifier StatementTail | IOStatement | IfStatement | WhileStatement
StatementTail -> : DeclarationTail | VariableTail = Expression

AssignStatement -> Variable = Expression
Factor -> ( Expression ) | Value

Expression -> Term ExpressionTail
ExpressionTail -> + Term ExpressionTail | - Term ExpressionTail | ε

Term -> Factor TermTail
TermTail -> * Factor TermTail | / Factor TermTail | % Factor TermTail | ε

IOStatement -> input ( Variable ) | output ( Expression )
IfStatement -> if ( Conditions ) CompoundStatement | if ( Conditions ) CompoundStatement else CompoundStatement
WhileStatement -> while ( Conditions ) CompoundStatement

Conditions -> Condition ConditionsTail
ConditionsTail -> \d Condition ConditionsTail | ε
Condition -> SimpleCondition ConditionTail
ConditionTail -> && SimpleCondition ConditionTail | ε

SimpleCondition -> Expression Relation Expression
Relation -> == | != | < | > | <= | >=
